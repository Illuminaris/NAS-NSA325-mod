U-boot for NSA325 is still experimental, but actually working.
You can get a copy here: http://forum.doozan.com/read.php?3,12381,17420#msg-17420

Please take usual care here: use UPS and MAKE SURE UART booting works before flashing!

Once you have flashed it, on serial access:

# reset Environment to default
env default -a

# Set new basic environmentm which will load from USB if present and then from HDD.
# For partition layout, adjust to your installation.

setenv arcNumber '4495'
setenv baudrate '115200'
setenv stderr 'serial'
setenv stdin 'serial'
setenv stdout 'serial'
setenv mainlineLinux 'yes'
setenv ethact 'egiga0'
setenv ethaddr '5C:F4:AB:40:5B:23'
setenv ethmtu '1500'
setenv ethprime 'egiga0'
setenv bootargs_end ':::orion:eth0:none'
setenv loadaddr '0x02000000'
setenv uInitramfsAddr '0x01100000'
setenv DTBAddr '0x10E0000'
setenv usb_load_uImage 'fatload usb 0:1 ${loadaddr} /boot/uImage || ext2load usb 0:1 ${loadaddr} /boot/uImage'
setenv usb_load_uInitramfs 'fatload usb 0:1 ${uInitramfsAddr} /boot/uInitramfs || ext2load usb 0:1 ${uInitramfsAddr} /boot/uInitramfs'
setenv usb_load_DTB 'fatload usb 0:1 ${DTBAddr} /boot/kirkwood-nsa325.dtb || ext2load usb 0:1 ${DTBAddr} /boot/kirkwood-nsa325.dtb'
setenv usb_setbootargs 'setenv bootargs console=ttyS0,115200 rootfstype=ext4 root=LABEL=usb-root usb-storage.delay_use=0 rootdelay=10 ${bootargs_end}'
setenv boot_from_usb 'usb reset; if run usb_load_DTB && run usb_load_uImage && run usb_load_uInitramfs; then run usb_setbootargs; bootm ${loadaddr} ${uInitramfsAddr} ${DTBAddr}; fi'
setenv hdd_load_uImage 'ext2load ide 0:3 ${loadaddr} /boot/uImage || ext2load ide 1:3 ${loadaddr} /boot/uImage'
setenv hdd_load_uInitramfs 'ext2load ide 0:3 ${uInitramfsAddr} /boot/uInitramfs || ext2load ide 1:3 ${uInitramfsAddr} /boot/uInitramfs'
setenv hdd_load_DTB 'ext2load ide 0:3 ${DTBAddr} /boot/kirkwood-nsa325.dtb || ext2load ide 1:3 ${DTBAddr} /boot/kirkwood-nsa325.dtb'
setenv hdd_setbootargs 'setenv bootargs console=ttyS0,115200 rootfstype=ext4 root=LABEL=hdd-root rootdelay=10 ${bootargs_end}'
setenv boot_from_hdd 'ide reset; if run hdd_load_DTB && run hdd_load_uImage && run hdd_load_uInitramfs; then run hdd_setbootargs; bootm ${loadaddr} ${uInitramfsAddr} ${DTBAddr}; fi'
setenv bootcmd 'run boot_from_usb || run boot_from_hdd'

